// Grammar for ViUR logics

#!mode          insensitive;
#prefix         "Logics";
#whitespaces    @whitespace ;

@whitespace     ' \r\n\t'+
                | "#" !'\n'* '\n'
                ;

@IDENT          'A-Za-z_' 'A-Za-z0-9_'*                             = IDENT;
@STRING         '"' ( '\\' . | !'"' )* '"'
                | '\'' ( '\\' . | !'\'' )* '\''                     = STRING;
@NUMBER         '0-9'+ '.' '0-9'* | '0-9'* '.' '0-9'+ | '0-9'+      = NUMBER;

logic$          : expression ;

expression      : test ;

test            : if_else
                | or_test
                ;

if_else         := or_test "if" or_test "else" test
                ;

or_test         : and_test ( "or" and_test )+                       = or_test
                | and_test
                ;

and_test        : not_test ( "and" not_test )+                      = and_test
                | not_test
                ;

not_test        : "not" not_test                                     = not_test
                | comparison
                ;

comparison_op   : ""==""
                | "">=""
                | ""<=""
                | ""<""
                | "">""
                | ""<>""
                | ""!=""
                | "in"                                              = in
                | "not" "in"                                        = not_in
                ;

comparison      : expr (comparison_op expr)+                        = cmp
                | expr
                ;

expr            : expr '+' term                                     = add
                | expr '-' term                                     = sub
                | term
                ;

term            : term '*' factor                                   = mul
                | term '/' factor                                   = div
                | term '%' factor                                   = mod
                | factor;

factor          : '+' factor                                        = pos
                | '-' factor                                        = neg
                | '~' factor                                        = invert
                | power
                ;

power           : entity "**" factor                                = pow
                | entity
                ;

entity          : atom trailer+                                     = entity
                | atom
                ;

opt_expression  : expression
                |                                                   = null
                ;

trailer         : '(' list ')'
                | '[' expression ']'
                | '[' opt_expression ':' opt_expression ']'         = slice
                | '.' @IDENT
                ;

atom            : ( ""True"" | ""False"" )
                | @NUMBER
                | @IDENT
                | @STRING
                | @STRING+                                          = concat
                | comprehension
                | '[' list ']'
                | '(' expression ')'                                = atom
                ;

comprehension   := '[' expression "for" @IDENT "in" or_test ( "if" expression )? ']'
                ;

list            := expression (',' expression )*
                |
                ;
